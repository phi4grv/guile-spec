#!/bin/sh
# vim: set filetype=scheme:
exec guile -s "$0" "$@"
!#

(eval-when (expand load eval)
  (define guile-spec-root
    (let ((f (current-filename)))
      (if f
        (dirname f)
        (getcwd))))

  (define guile-spec-dir
    (case-lambda
      (() guile-spec-root)
      ((path) (in-vicinity guile-spec-root path))))

  (add-to-load-path
    (guile-spec-dir "module")))

(use-modules (ice-9 getopt-long)
             (srfi srfi-171)
             (spec suite))

(define (option-values opts key)
  (list-transduce
    (compose
      (tfilter (lambda (p) (eq? (car p) key)))
      (tmap cdr))
    reverse-rcons
    opts))

(define option-spec
  '((version (single-char #\v) (value #f))
    (help    (single-char #\h) (value #f))
    (filter  (single-char #\f) (value #t))
    (load-path (single-char #\L) (value #t))))

(define options (getopt-long (command-line) option-spec))

(define (ensure-abspath path)
  (or (and (absolute-file-name? path) path)
      (in-vicinity (getcwd) path)))

(for-each
  (lambda (p)
    (add-to-load-path (ensure-abspath p)))
  (option-values options 'load-path))

(define (load-from-cwd path)
  (load-in-vicinity (getcwd) path))

(define (run-specs)
  (let* ((filter  (option-ref options 'filter #f))
         (files   (option-ref options '() '()))
         (suites  (map load-from-cwd files))
         (suites* (spec-filter filter suites)))
    (spec-run suites*)))

(define (main)
  (define (help-wanted?)
    (option-ref options 'help #f))
  (define (version-wanted?)
    (option-ref options 'version #f))
  (cond ((help-wanted?)
         (display "guile-spec [options] [files...]\n")
         (display "\nOptions:\n")
         (display "  -f, --filter <regex>   Filter specs by regex on spec descriptions\n")
         (display "  -L, --load-path <path> Add <path> to Guile load path\n")
         (exit))
        ((version-wanted?)
         (display "guile-spec version 0.1")
         (exit)))
  (run-specs))

(main)
